plugins {
    id "de.undercouch.download" version "4.1.2"
}

maturity = 'EXPERIMENTAL'

ext {
    //TODO: when changed, adjust SpatiaLiteLoaderImpl.getName (can we inject it?)
    spatialiteVersion = '5.0.1'
    buildVersion = '2'
    generatedResourcesDir = new File(buildDir, 'generated/src/main/resources/')
    embeddedLibDir = new File(generatedResourcesDir, "spatialite-${spatialiteVersion}-${buildVersion}/lib")
}

project.sourceSets.main.output.dir(generatedResourcesDir)

apply from: 'zlib.gradle'
apply from: 'sqlite.gradle'
apply from: 'geos.gradle'
apply from: 'spatialite.gradle'

dependencies {
    provided project(':xtraplatform-nativ-loader')
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

moduleInfo {

}

project.afterEvaluate {
    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://dl.interactive-instruments.de/repository/maven-releases/"
                def snapshotsRepoUrl = "https://dl.interactive-instruments.de/repository/maven-snapshots/"

                name = 'projJni'
                url project.parent.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.findProperty('deployUser') ?: ''
                    password project.findProperty('deployPassword') ?: ''
                }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    // The value 'enforced-platform' is provided in the validation
    // error message you got
    suppressedValidationErrors.add('enforced-platform')
}
